program sfchdr
!$$$  main program documentation block
!
! Main program: sfchdr       Print information from surface header
!   Prgmmr: Iredell          Org: np23        Date: 1999-08-23
!
! Abstract: This program prints information from the surface header.
!   The following parameters may be printed out:
!     filetype
!     fhour
!     ifhr
!     idate
!     iyr
!     imo
!     idy
!     ihr
!     vdate
!     vyr
!     vmo
!     vdy
!     vhr
!     latb
!     lonb
!     ivsn
!     lonsperlat
!
! Program history log:
! 1999-08-23  Iredell
!
! Input files:
!   arg.   1     surface file(s)
!
! Subprograms called:
!   iargc
!   errmsg
!   eusage
!   errexit
!   getarg
!   sfcvar
!
! Attributes:
!   Language: fortran90
!
!$$$
  implicit none
  integer narg,iargc
  integer,parameter:: lusfc=11
  character(255) cfsfc
  character(16) cvar
  real fhour
  integer idate(4),lonb,latb,ivsn
  integer,allocatable:: lpl(:)
  integer ncfsfc,ios
  narg=iargc()
  if(narg.lt.1.or.narg.gt.2) then
     if(narg.ne.0) call errmsg('sfchdr: too many arguments')
     call eusage
     call errexit(1)
  endif
  call getarg(1,cfsfc)
  ncfsfc=len_trim(cfsfc)
  open(lusfc,file=cfsfc(1:ncfsfc),form='unformatted',status='old',iostat=ios)
  if(ios.ne.0) then
     call errmsg('sfchdr: error opening file '//cfsfc(1:ncfsfc))
     call errexit(2)
  endif
  read(11,iostat=ios)
  read(11,iostat=ios) fhour,idate,lonb,latb,ivsn
  if(ios.ne.0) then
    call errmsg('sfchdr: error reading header from file '//cfsfc(1:ncfsfc))
    call errexit(2)
  endif
  allocate(lpl(latb/2))
  if(ivsn.ge.200004) then
    rewind 11
    read(11,iostat=ios)
    read(11,iostat=ios) fhour,idate,lonb,latb,ivsn,lpl
    if(ios.ne.0) then
      call errmsg('sfchdr: error reading header from file '//cfsfc(1:ncfsfc))
      call errexit(2)
    endif
  else
    lpl=lonb
  endif
  if(narg.eq.2) then
    call getarg(2,cvar)
    call sfcvar(fhour,idate,lonb,latb,ivsn,lpl,cvar)
  else
    do
      read(5,*,iostat=ios) cvar
      if(ios.ne.0) exit
      call sfcvar(fhour,idate,lonb,latb,ivsn,lpl,cvar)
    enddo
  endif
end program
subroutine sfcvar(fhour,idate,lonb,latb,ivsn,lpl,cvar)
  implicit none
  real,intent(in):: fhour
  integer,intent(in):: idate(4),lonb,latb,ivsn,lpl(latb/2)
  character(16),intent(in):: cvar
  integer lval
  character(16) cval
  integer jdat(8)
  select case(cvar)
  case('FILETYPE','filetype')
    print '(a)','sfc'
  case('FHOUR','fhour')
    call inch(int(fhour),lval,cval)
    print '(a,f3.2)',cval(1:lval),fhour-int(fhour)
  case('IFHR','ifhr')
    call inch(int(fhour),lval,cval)
    if(lval.le.1) then
      print '(a)','0'//cval(1:lval)
    else
      print '(a)',cval(1:lval)
    endif
  case('IDATE','idate')
    print '(i4.4,3i2.2)',idate(4),idate(2),&
                         idate(3),idate(1)
  case('IYR','iyr')
    call inch(idate(4),lval,cval)
    print '(a)',cval(1:lval)
  case('IMO','imo')
    call inch(idate(2),lval,cval)
    print '(a)',cval(1:lval)
  case('IDY','idy')
    call inch(idate(3),lval,cval)
    print '(a)',cval(1:lval)
  case('IHR','ihr')
    call inch(idate(1),lval,cval)
    print '(a)',cval(1:lval)
  case('VDATE','vdate')
    call w3movdat((/0.,fhour,0.,0.,0./),&
                  (/idate(4),idate(2),idate(3),0,&
                    idate(1),0,0,0/),jdat)
    print '(i4.4,3i2.2)',jdat(1),jdat(2),jdat(3),jdat(5)
  case('VYR','vyr')
    call w3movdat((/0.,fhour,0.,0.,0./),&
                  (/idate(4),idate(2),idate(3),0,&
                    idate(1),0,0,0/),jdat)
    call inch(jdat(1),lval,cval)
    print '(a)',cval(1:lval)
  case('VMO','vmo')
    call w3movdat((/0.,fhour,0.,0.,0./),&
                  (/idate(4),idate(2),idate(3),0,&
                    idate(1),0,0,0/),jdat)
    call inch(jdat(2),lval,cval)
    print '(a)',cval(1:lval)
  case('VDY','vdy')
    call w3movdat((/0.,fhour,0.,0.,0./),&
                  (/idate(4),idate(2),idate(3),0,&
                    idate(1),0,0,0/),jdat)
    call inch(jdat(3),lval,cval)
    print '(a)',cval(1:lval)
  case('VHR','vhr')
    call w3movdat((/0.,fhour,0.,0.,0./),&
                  (/idate(4),idate(2),idate(3),0,&
                    idate(1),0,0,0/),jdat)
    call inch(jdat(5),lval,cval)
    print '(a)',cval(1:lval)
  case('LATB','latb')
    call inch(latb,lval,cval)
    print '(a)',cval(1:lval)
  case('LONB','lonb')
    call inch(lonb,lval,cval)
    print '(a)',cval(1:lval)
  case('IVSN','ivsn')
    call inch(ivsn,lval,cval)
    print '(a)',cval(1:lval)
  case('LONSPERLAT','lonsperlat')
    print '(i6)',latb/2
    print '(10i6)',lpl
  case('?')
    print '(a)','Choose from:'
    print '(a)','  filetype'
    print '(a)','  fhour'
    print '(a)','  ifhr'
    print '(a)','  idate'
    print '(a)','  iyr'
    print '(a)','  imo'
    print '(a)','  idy'
    print '(a)','  ihr'
    print '(a)','  vdate'
    print '(a)','  vyr'
    print '(a)','  vmo'
    print '(a)','  vdy'
    print '(a)','  vhr'
    print '(a)','  latb'
    print '(a)','  lonb'
    print '(a)','  ivsn'
    print '(a)','  lonsperlat'
  case default
    print '(a)','?'
  end select
end subroutine
subroutine inch(i,l,c)
  implicit none
  integer,intent(in):: i
  integer,intent(out):: l
  character(*),intent(out):: c
  character*20 cform
  l=log10(abs(i)+0.5)+1
  if(i.le.0) l=l+1
  write(cform,'("(i",i1,")")') l
  write(c,cform) i
end subroutine
subroutine eusage
  implicit none
  call errmsg('Usage: sfchdr sfcfile <variable.list >value.list')
  call errmsg('   or  sfchdr sfcfile variable >value')
end subroutine
